 

#include "glsl_globals.h"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

#ifdef VULKAN
layout(binding = 0) uniform PRECISION sampler2DMSArray depthMS;
layout(binding = 1) uniform PRECISION usampler2DMSArray stencilMS;
#else
layout(binding = 0) uniform PRECISION sampler2DMS depthMS;
layout(binding = 1) uniform PRECISION usampler2DMS stencilMS;
#endif

layout(binding = 2, std140) writeonly buffer pixelhistorydest
{
  uvec4 result[];
}
dest;

#ifdef VULKAN
layout(push_constant) uniform multisamplePush
#else
layout(std140, binding = 3) uniform multisamplePush
#endif
{
  int currentSample;
  int x;
  int y;
  int dstOffset;
  int hasDepth;
  int hasStencil;
}
mscopy;

#define currentSample (mscopy.currentSample)
#define x (mscopy.x)
#define y (mscopy.y)
#define dstOffset (mscopy.dstOffset)
#define hasDepth (mscopy.hasDepth)
#define hasStencil (mscopy.hasStencil)

void main()
{
#ifdef VULKAN
  ivec3 loc = ivec3(x, y, 0);
#else
  ivec2 loc = ivec2(x, y);
#endif
  float depth = 0.0;
  if(hasDepth == 1)
    depth = texelFetch(depthMS, loc, currentSample).r;
  uint stencil = 0u;
  if(hasStencil == 1)
    stencil = texelFetch(stencilMS, loc, currentSample).r;
  dest.result[dstOffset] = uvec4(floatBitsToUint(depth), stencil, 0, 0);
}
