 

#if defined(GLES)
#extension GL_OES_texture_storage_multisample_2d_array : require
#elif defined(OPENGL_CORE)
#extension GL_ARB_compute_shader : require

// safe to assume this extension in compute shaders as it pre-dates compute shaders
#extension GL_ARB_shading_language_420pack : require
#endif

#include "glsl_globals.h"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(binding = 0) uniform PRECISION usampler2DArray srcArray;
// binding = 1 used as stencil read in the depth-stencil copy fragment shaders

#ifdef VULKAN

layout(binding = 2) writeonly uniform PRECISION uimage2DMSArray dstMS;

layout(push_constant) uniform multisamplePush
{
  int numMultiSamples;
  int sampleOffset;
  int sliceOffset;
  uint currentStencil;
}
mscopy;

#define numMultiSamples (mscopy.numMultiSamples)
#define sampleOffset (mscopy.sampleOffset)
#define sliceOffset (mscopy.sliceOffset)
#define currentStencil (mscopy.currentStencil)

#else

layout(rgba32ui, binding = 2) writeonly uniform PRECISION uimage2DMSArray dstMS;

uniform ivec4 mscopy;

#define numMultiSamples (mscopy.x)
#define sampleOffset (mscopy.y)
#define sliceOffset (mscopy.z)
#define currentStencil (mscopy.w)

#endif

void main()
{
  ivec3 id = ivec3(gl_GlobalInvocationID);

  int slice = sliceOffset + int(id.z / numMultiSamples);
  int sampleIdx = sampleOffset + int(id.z % numMultiSamples);

  uvec4 data =
      texelFetch(srcArray, ivec3(int(id.x), int(id.y), slice * numMultiSamples + sampleIdx), 0);

  imageStore(dstMS, ivec3(int(id.x), int(id.y), slice), sampleIdx, data);
}
